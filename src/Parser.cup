import java_cup.runtime.*;

import java.util.*;
import java.util.function.*;
import java.io.*;

/* TERMINAL */
terminal
	ASSIGNMENT_OP, 
	DOLLAR_OP, 
	L_PAREN, R_PAREN,
	PIPE,
	REDIR_W, REDIR_A
;

terminal String
	COMMAND, TOKEN, STRING, S_END, SP
;

/* NON TERMINAL */
non terminal 
	program
;

non terminal List<Data.Result>
	statement_list
;

non terminal Data.Result
	statement, command
;

non terminal String
	sp_or_nothing
;

non terminal List<String>
	expression_list_or_nothing, 
	expression_list
;

non terminal String
	expression
;

/* PRODUCTION */
program ::=
	statement_list:s_list
	{:
		for (var res : s_list) {
			if (res.code == 0) {
				System.out.print(res.value);
			}
		}
	:}
;

statement_list ::=
	statement:s
	{:
		RESULT = new ArrayList<Data.Result>();
		RESULT.add(s); 
	:}
	|
	statement_list:s_list S_END sp_or_nothing statement:s
	{:
		s_list.add(s);
		RESULT = s_list;
	:}
	|
	statement_list:s_list S_END
	{:
		RESULT = s_list;
	:}
;

statement ::=
	command:c sp_or_nothing
	{:
		RESULT = c;
	:}
	|
	expression:name ASSIGNMENT_OP expression:value
	{:
		RESULT = Data.Operators.assign(name, value);
	:}
;

command ::=
	COMMAND:c SP expression_list_or_nothing:args
	{:
		RESULT = Data.commands.get(c).apply(null, args);
	:}
	|
	command:prev sp_or_nothing PIPE sp_or_nothing COMMAND:c SP expression_list_or_nothing:args
	{:
		RESULT = Data.commands.get(c).apply(prev, args);
	:}
	|
	command:c sp_or_nothing REDIR_W sp_or_nothing expression_list:files
	{:
		RESULT = Data.Redirection.redirectWrite(c, files);
	:}
	|
	command:c sp_or_nothing REDIR_A sp_or_nothing expression_list:files
	{:
		RESULT = Data.Redirection.redirectAppend(c, files);
	:}
;

sp_or_nothing ::=
	{: 
		RESULT = "";
	:}
	|
	SP:s
	{:
		RESULT = s;
	:}
;


expression_list_or_nothing ::=
	{:
		RESULT = new ArrayList<String>();
	:}
	|
	expression_list:e_list
	{:
		RESULT = e_list;
	:}
;

expression_list ::=
	expression:e
	{:
		RESULT = new ArrayList<String>();
		RESULT.add(e);
	:}
	|
	expression_list:e_list expression:e
	{:
		e_list.add(e);
		RESULT = e_list;
	:}
;

/* Not here */
expression ::= 
	TOKEN:t
	{: RESULT = t; :}
	|
	STRING:s 
	{:
		RESULT = s.replace("\\\\", "\\").replace("\\\"", "\"");
	:}
	|
	DOLLAR_OP L_PAREN statement_list:s_list R_PAREN
	{: 
		var result = new StringBuilder();
		for (var res : s_list) {
			result.append(res.value.strip()).append(" ");
		}
		RESULT = result.toString().strip();
	:}
	|
	DOLLAR_OP expression:e
	{:
		RESULT = Data.Operators.getVariable(e);
	:}
;







