import java_cup.runtime.*;

import java.util.*;
import java.util.function.*;
import java.io.*;

/* TERMINAL */
terminal
	ASSIGNMENT_OP, 
	DOLLAR_OP, 
	L_PAREN, R_PAREN, 
	REDIR_W, REDIR_A, 
	ECHO, CAT, WC
;

terminal String
	TOKEN, S_END
;

/* NON TERMINAL */
non terminal 
	program
;

non terminal List<Data.Result>
	statement_list
;

non terminal Data.Result
	statement
;

non terminal Data.Result
	call
;

non terminal List<String>
	expression_list
;

non terminal String
	expression
;

/* PRODUCTION */
program ::=
	statement_list:s_list
	{:
		for (var res : s_list) {
			System.out.print(res.value);
		}
	:}
;

statement_list ::=
	statement:s
	{:
		RESULT = new ArrayList<Data.Result>();
		RESULT.add(s); 
	:}
	|
	statement_list:s_list statement:s
	{:
		s_list.add(s);
		RESULT = s_list;
	:}
;

statement ::=
	call:c S_END
	{: 
		RESULT = c;
	:}
;

call ::=
	ECHO expression_list:e_list
	{:
		RESULT = Data.Commands.echo(e_list);
	:}
;

expression_list ::=
	expression:e
	{:
		RESULT = new ArrayList<String>();
		RESULT.add(e);
	:}
	|
	expression_list:e_list expression:e
	{:
		e_list.add(e);
		RESULT = e_list;
	:}
;

expression ::=
	TOKEN:t
	{: RESULT = t; :}
	|
	DOLLAR_OP L_PAREN statement_list:s_list R_PAREN
	{: 
		var result = new StringBuilder();
		for (var res : s_list) {
			result.append(res.value.strip()).append(" ");
		}
		RESULT = result.toString().strip();
	:}
;







