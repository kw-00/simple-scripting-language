import java_cup.runtime.*;

import java.util.*;
import java.io.*;

/* TERMINAL */
terminal
	ECHO, CAT, WC, REDIR_W, REDIR_A
;

terminal String
	TOKEN, S_END
;

/* NON TERMINAL */
non terminal 
	statement_list, statement
;
non terminal String
	call
;

non terminal String
	token_list
;


/* PRODUCTION */
statement_list ::=
	statement
	| statement_list statement
;

statement ::=
	call:c S_END
	{: System.out.print(c); :}
	|
	call:c REDIR_W token_list:files S_END
	{:
		for (var path: files.split(" ")) {
			var out = new PrintWriter(path);
			out.print(c);
			out.flush();
		}
	:}
	|
	call:c REDIR_A token_list:files S_END
	{:
		for (var path: files.split(" ")) {
			var out = new PrintWriter(new FileWriter(path, true));
			out.print(c);
			out.flush();
		}
	:}
;

call ::=
	ECHO
	{: RESULT = "\n"; :}
	|
	ECHO token_list:tl
	{: RESULT = tl + "\n"; :}
	|
	CAT token_list:tl
	{:
		var result = new StringBuilder();
		for (var token: tl.split(" ")) {
			var file = new File(token);
			var scanner = new java.util.Scanner(file);
			while (scanner.hasNextLine()) {
				result.append(scanner.nextLine()).append("\n");
			}
			result.append("\n");
		}
		RESULT = result.toString();
	:}
	|
	WC token_list:tl
	{: 
		var result = new StringBuilder();
		for (var token: tl.split(" ")) {
			var count = 0;
			var file = new File(token);
			var scanner = new java.util.Scanner(file);
			while (scanner.hasNextLine()) {
				scanner.nextLine();
				count++;
			}
			result.append(count).append(":");
		}
		RESULT = result.delete(result.length() - 1, result.length()).append("\n").toString();
	:}
;

token_list ::=
	TOKEN:t 
	{: RESULT = t; :}
	| 
	token_list:tl TOKEN:t
	{: RESULT = tl + " " + t; :}
;






