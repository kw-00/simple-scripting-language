import java_cup.runtime.*;

import java.util.*;
import java.util.function.*;
import java.io.*;

/* TERMINAL */
terminal
	ASSIGNMENT_OP, 
	DOLLAR_OP, 
	L_PAREN, R_PAREN, 
	REDIR_W, REDIR_A
;

terminal String
	COMMAND, TOKEN, S_END
;

/* NON TERMINAL */
non terminal 
	program
;

non terminal List<Data.Result>
	statement_list
;

non terminal Data.Result
	statement, command
;

non terminal List<String>
	expression_list
;

non terminal String
	expression
;

/* PRODUCTION */
program ::=
	statement_list:s_list
	{:
		for (var res : s_list) {
			System.out.print(res.value);
		}
	:}
;

statement_list ::=
	statement:s
	{:
		RESULT = new ArrayList<Data.Result>();
		RESULT.add(s); 
	:}
	|
	statement_list:s_list S_END statement:s
	{:
		s_list.add(s);
		RESULT = s_list;
	:}
	|
	statement_list:s_list S_END
	{:
		RESULT = s_list;
	:}
;

statement ::=
	command:c
	{:
		RESULT = c;
	:}
	|
	expression:name ASSIGNMENT_OP expression:value
	{:
		RESULT = Data.Operators.assign(name, value);
	:}
;

command ::=
	COMMAND:c
	{:
		RESULT = Data.commands.get(c).apply(null, null);
	:}
	|
	COMMAND:c expression_list:args
	{:
		RESULT = Data.commands.get(c).apply(null, args);
	:}
	|
	COMMAND:c expression_list:args REDIR_W expression_list:files
	{:
		var commandResult = Data.commands.get(c).apply(null, args);
		RESULT = Data.Redirection.redirectWrite(commandResult, files);
	:}
	|
	COMMAND:c expression_list:args REDIR_A expression_list:files
	{:
		var commandResult = Data.commands.get(c).apply(null, args);
		RESULT = Data.Redirection.redirectAppend(commandResult, files);
	:}
;

expression_list ::=
	expression:e
	{:
		RESULT = new ArrayList<String>();
		RESULT.add(e);
	:}
	|
	expression_list:e_list expression:e
	{:
		e_list.add(e);
		RESULT = e_list;
	:}
;

/* Not here */
expression ::= 
	TOKEN:t
	{: RESULT = t; :}
	|
	DOLLAR_OP L_PAREN statement_list:s_list R_PAREN
	{: 
		var result = new StringBuilder();
		for (var res : s_list) {
			result.append(res.value.strip()).append(" ");
		}
		RESULT = result.toString().strip();
	:}
	|
	DOLLAR_OP expression:e
	{:
		RESULT = Data.Operators.getVariable(e);
	:}
;







