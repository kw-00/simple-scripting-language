import java_cup.runtime.*;

import java.util.*;
import java.util.function.*;
import java.io.*;

/* TERMINAL */
terminal
	ASSIGNMENT_OP, VAR_OP, REDIR_W, REDIR_A, ECHO, CAT, WC
;

terminal String
	TOKEN, S_END
;

/* NON TERMINAL */
non terminal 
	statement_list, statement
;
non terminal State.Result
	call
;

non terminal List<String>
	token_list
;


/* PRODUCTION */
statement_list ::=
	statement
	| 
	statement_list statement
;

statement ::=
	call:c S_END
	{: 
		if (c.code > 0) {
			System.err.println(c.value);
		} else {
			System.out.print(c.value); 
		}
	:}
	|
	call:c REDIR_W token_list:files S_END
	{:
		if (c.code > 0) {
			System.err.println(c.value);
		} else {
			for (var path : files) {
				var out = new PrintWriter(path);
				out.print(c.value);
				out.flush();
			}
		}
	:}
	|
	call:c REDIR_A token_list:files S_END
	{:
		if (c.code > 0) {
			System.err.println(c.value);
		} else {
			for (var path : files) {
				var out = new PrintWriter(new FileWriter(path, true));
				out.print(c.value);
				out.flush();
			}
		}
	:}
;

call ::=
	TOKEN:name ASSIGNMENT_OP token_list:value 
	{:
		var result = new StringBuilder();
		for (var el : value) {
			result.append(el).append(" ");
		}
		var finalResult = result.toString().strip();
		State.variables.put(name, finalResult);
		RESULT = State.Result.success(finalResult);
	:}
	|
	ECHO
	{: RESULT = State.Result.success("\n"); :}
	|
	ECHO token_list:tl
	{: 
		var result = new StringBuilder();
		for (var token : tl) {
			result.append(token).append(" ");
		}
		result.append("\n");
		RESULT = State.Result.success(result.toString());
	:}
	|
	CAT token_list:files
	{:
		var result = new StringBuilder();
		for (var token : files) {
			var file = new File(token);
			var scanner = new java.util.Scanner(file);
			while (scanner.hasNextLine()) {
				result.append(scanner.nextLine()).append("\n");
			}
			result.append("\n");
		}
		RESULT = State.Result.success(result.toString());
	:}
	|
	CAT
	{: RESULT = State.Errors.NoArgs.get(); :}
	|
	WC token_list:files
	{: 
		var result = new StringBuilder();
		for (var token : files) {
			var count = 0;
			var file = new File(token);
			var scanner = new java.util.Scanner(file);
			while (scanner.hasNextLine()) {
				scanner.nextLine();
				count++;
			}
			result.append(count).append(":");
		}
		RESULT = State.Result.success(result.delete(result.length() - 1, result.length()).append("\n").toString());
	:}
	|
	WC
	{: RESULT = State.Errors.NoArgs.get(); :}
;

token_list ::=
	TOKEN:t 
	{: RESULT = Arrays.asList(t); :}
	| 
	token_list:tl TOKEN:t
	{: 
		tl.add(t);
		RESULT = tl; 
	:}
;






